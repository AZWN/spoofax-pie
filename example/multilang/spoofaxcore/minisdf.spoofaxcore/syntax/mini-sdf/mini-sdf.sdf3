module mini-sdf/mini-sdf

imports

  Common

lexical sorts
  MODNAME KW ID

context-free sorts
  MSDFStart MSDFSection
  SortTerm SortRef
  ProdTerm Production

lexical syntax
  MODNAME 		= [a-zA-Z0-9\-\/]+
  KW			= ~[\<\>\ ]+
  ID			= [a-zA-Z] [a-zA-Z0-9\-]*

  MODNAME = "context-free" 		{reject}
  MODNAME = "rules" 			{reject}
  MODNAME = "signatures"	 	{reject}
  MODNAME = "sorts"				{reject}

lexical restrictions
  KW	 		-/- ~[\<\>\ \r]
  MODNAME	 	-/- [a-zA-Z0-9\-\/]
  ID 			-/- [a-zA-Z0-9\-]

context-free syntax

  MSDFStart.MSDFModule = <
    module <MODNAME>
      <{MSDFSection "\n"}*>
    >

  MSDFSection.ImportSection = <
  	imports
    	<{MODNAME "\n"}*>
    >

  MSDFSection.SortsDecl = <
  	sorts
    	<{ID "\n"}*>
    >

  MSDFSection.ContextFreeSyntax = <
  	context-free syntax
    	<{Production "\n"}*>
    >

  Production.Production = [
  	[ID].[ID] = <[{ProdTerm " "}*]>
  ]

  ProdTerm.Terminal = <<KW>>
  ProdTerm.SortTerm = [<[SortTerm]>]

  SortTerm.Plus 	= <<SortTerm>+> 	{non-assoc}
  SortTerm.Option	= <<SortTerm>?>		{non-assoc}
  SortTerm.IterStar	= <<SortTerm>*>		{non-assoc}
  SortTerm.Ref		= SortRef

  SortRef.SortRef   = <<ID>>
