module module-interface/modules

imports
  cons-type-interface/types
  cons-type-interface/labels

signature

  namespaces
    Module : string

  sorts MODULE constructors
    MODULE : scope * ModuleType -> MODULE

  sorts ModuleType // Extension point to maintain data for import validating
  
  relations
    modType : occurrence -> MODULE

  name-resolution

    resolve Module filter P*

rules

  modTypesOk: ModuleType * ModuleType
  modTypesOk(t, t).

  // Rule used for validating unique import of sorts/conss/rules
  itemsOk: string * ModuleType * ModuleType * scope * scope

rules

  declareMod: scope * string * scope * ModuleType

  declareMod(s, n, s_mod, T) :-
    s -> Module{n} with modType MODULE(s_mod, T),
    // Validate module name unique
    modType of Module{n} in s |-> [_]
      | error $[Module [n] declared multiple times].

rules

  import: scope * string * ModuleType

  import(s_imp, n, T) :- {s_mod T1}
    s_imp -I-> s_mod,
    modType of Module{n} in s_imp |-> [(_, (_, MODULE(s_mod, T1))) | _]
        | error $[Module [n] could not be found],
    // Validate module type combination is correct.
    try { modTypesOk(T, T1) }
        | error $[Module of type [T] cannot import module of type [T1]],
    // Validate no comflicts in imported constructs
    itemsOk(n, T, T1, s_imp, s_mod).