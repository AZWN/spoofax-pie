module cfg

imports
  
  lex/literal
  lex/layout
  lex/comment
  
  literal

context-free start-symbols
  
  Start

context-free syntax
  
  Start.Sections = <<{Section "\n"}*>>

context-free syntax // Workspace config

  Section.WorkspaceSec =
<workspace
  <{WorkspaceOpt "\n"}*>
>
  WorkspaceOpt.LangSpec = <langspec <Path>>
  WorkspaceOpt.SpxLang = <spxlang <Path>>
  WorkspaceOpt.SpxLangSpec = <spxlangspec <Path>>

context-free syntax // Language specification config

  Section.LangSpecSec =
<langspec
  <{LangSpecSubSec "\n"}*>
>

  LangSpecSubSec.IdentificationSec =
<identification
  <{IdentificationOpt "\n"}*>
>  
  IdentificationOpt.FileExtension = <file extension: <EXT>>

  LangSpecSubSec.InformationSec =
<information
  <{InformationOpt "\n"}*>
>
  InformationOpt.Name = <name: <String>>

  LangSpecSubSec.SyntaxSec =
<syntax
  <{SyntaxOpt "\n"}*>
>
  SyntaxOpt.SyntaxMainFile = <main file: <Path>>
  SyntaxOpt.SyntaxStartSymbol = <start symbol: <ID>>

  LangSpecSubSec.StylingSec =
<styling
  <{StylingSubSec "\n"}*>
>
  StylingSubSec.SyntaxBasedStylingSubSec =
<syntax-based
  <{SyntaxBasedStylingOpt "\n"}*>
> 
  SyntaxBasedStylingOpt.SyntaxBasedStylingFile = <file: <Path>>
