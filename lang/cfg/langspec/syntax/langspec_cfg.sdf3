module langspec_cfg

imports

  cfg
  literal

context-free syntax // Language specification config

  Section.LangSpecSec =
<langspec {
  <{LangSpecSubSec "\n"}*>
}>

context-free syntax // Identification

  LangSpecSubSec.IdentificationSec =
<identification {
  <{IdentificationOpt "\n"}*>
}>  
  IdentificationOpt.LanguageIdentifier = <id: <String>>
  IdentificationOpt.FileExtensions = <file extensions: <Strings>>

context-free syntax // Information

  LangSpecSubSec.InformationSec =
<information {
  <{InformationOpt "\n"}*>
}>
  InformationOpt.Name = <name: <String>>

context-free syntax // Syntax

  LangSpecSubSec.SyntaxSec =
<syntax {
  <{SyntaxSubSec "\n"}*>
}>

  SyntaxSubSec.SyntaxParseSubSec =
<parse {
  <{SyntaxParseOpt "\n"}*>
}> 
  SyntaxParseOpt.SyntaxParseFiles = <files: <Strings>>
  SyntaxParseOpt.SyntaxParseMainFile = <main file: <String>>
  SyntaxParseOpt.SyntaxParseStartSymbolId = <start symbol id: <String>>

  SyntaxSubSec.SyntaxSignatureSubSec =
<signature {
  <{SyntaxSignatureOpt "\n"}*>
}> 
  SyntaxSignatureOpt.SignatureSyntaxFiles = <files: <Strings>>

  SyntaxSubSec.SyntaxStyleSubSec =
<style {
  <{SyntaxStyleOpt "\n"}*>
}> 
  SyntaxStyleOpt.SyntaxStyleFile = <file: <String>>

context-free syntax // Names and Types

  LangSpecSubSec.NaTsSec = 
<names and types {
  <{NaTsOpt "\n"}*>
}>

  NaTsOpt.NaTsNaBL2Files = <nabl2 files: <Strings>>
  NaTsOpt.NaTsStrategoConfig = <stratego config: <StrategoConfig>>
  NaTsOpt.NaTsStrategoStrategyId = <stratego strategy id: <String>>
  NaTsOpt.NaTsRootScopePerFile = <root scope per file: <Bool>>

context-free syntax // Stratego config object

  StrategoConfig.StrategoConfig =
<{
  <{StrategoConfigOpt "\n"}*>
}>
  StrategoConfigOpt.StrategoConfigMainFile = <main file: <String>>
  StrategoConfigOpt.StrategoConfigIncludeDirs = <include dirs: <Strings>>
  StrategoConfigOpt.StrategoConfigIncludeFiles = <include files: <Strings>>
  StrategoConfigOpt.StrategoConfigIncludeLibs = <include libs: <Strings>>
  StrategoConfigOpt.StrategoConfigBaseDir = <base dir: <String>>
  StrategoConfigOpt.StrategoConfigCacheDir = <cache dir: <String>>
  StrategoConfigOpt.StrategoConfigOutputFile = <output file: <String>>
  